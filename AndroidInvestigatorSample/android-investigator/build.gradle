apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.lemonboston'
version = '0.1.1'

def snapshotRepo = 'https://oss.sonatype.org/content/repositories/snapshots'
def stagingRepo = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'


sourceCompatibility = 1.6
targetCompatibility = 1.6

tasks.javadoc.options {
    links 'http://docs.oracle.com/javase/6/docs/api/'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: stagingRepo) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'android-investigator'
                packaging 'jar'
                description 'Simple tool for adding informative debug logs quickly.'
                url 'https://bitbucket.org/lemonboston/android-investigator'

                scm {
                    url 'https://bitbucket.org/lemonboston/android-investigator.git'
                    connection 'scm:git:https://lemonboston@bitbucket.org/lemonboston/android-investigator.git'
                    developerConnection 'scm:git:https://lemonboston@bitbucket.org/lemonboston/android-investigator.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lemonboston'
                        name 'Gabor Keszthelyi'
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}
